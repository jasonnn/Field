apply plugin: 'build-dashboard'


subprojects {
    if (name == 'plugins') return

    apply plugin: 'java'
    apply plugin: 'project-report'

    sourceCompatibility = 1.6
    version = '1.0'

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    dependencies {
        compile 'org.jetbrains:annotations:13.0'
        testCompile group: 'junit', name: 'junit', version: '4.11'
    }
}

DependencyHandler.metaClass.proj = { String name ->
    if (!name.startsWith(':')) name = ':' + name
    compile(project(name)) {
        transitive = false
    }
}

project('core') {
    dependencies {
        proj(':annotations')
        proj(':asm')
        proj(':util')
        proj(':core-api')
    }

}
project('instrumentation-rt') {
    dependencies {
        proj(':core-api')
        proj(':util')
        proj(':asm')
        proj(':annotations')
    }
}
project('instrumentation-asm') {
    dependencies {
        proj(':core-api')
        proj(':instrumentation-rt')
        proj(':asm')
        proj(':util')
    }
}
project('instrumentation-aspectj') {
    dependencies {
        proj(':core-api')
        proj(':instrumentation-rt')
        proj(':asm')
        proj(':util')
    }
}
project('asm') {
    dependencies {
        proj(':util')
    }
}
project('annotation-processing') {
    dependencies {
        compile project(':annotations')
    }
}

project('bootstrap') {
    dependencies {
        runtime project(':core')
    }
}
